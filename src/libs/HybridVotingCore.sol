// SPDX-License-Identifier: AGPL-3.0-only
pragma solidity ^0.8.30;

import "../HybridVoting.sol";
import "./VotingErrors.sol";
import "./VotingMath.sol";
import {IExecutor} from "../Executor.sol";
import {IHats} from "lib/hats-protocol/src/Interfaces/IHats.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

library HybridVotingCore {
    bytes32 private constant _STORAGE_SLOT = keccak256("poa.hybridvoting.v2.storage");

    event VoteCast(
        uint256 indexed id,
        address indexed voter,
        uint8[] idxs,
        uint8[] weights,
        uint256[] classRawPowers,
        uint64 timestamp
    );
    event Winner(uint256 indexed id, uint256 indexed winningIdx, bool valid, bool executed, uint64 timestamp);
    event ProposalExecuted(uint256 indexed id, uint256 indexed winningIdx, uint256 numCalls);
    event ProposalExecutionFailed(uint256 indexed id, uint256 indexed winningIdx, bytes reason);

    function _layout() private pure returns (HybridVoting.Layout storage s) {
        bytes32 slot = _STORAGE_SLOT;
        assembly {
            s.slot := slot
        }
    }

    function vote(uint256 id, uint8[] calldata idxs, uint8[] calldata weights) external {
        if (idxs.length != weights.length) revert VotingErrors.LengthMismatch();
        if (block.timestamp > _layout()._proposals[id].endTimestamp) revert VotingErrors.VotingExpired();

        HybridVoting.Layout storage l = _layout();
        HybridVoting.Proposal storage p = l._proposals[id];
        address voter = msg.sender;

        // Check poll-level restrictions
        if (p.restricted) {
            bool hasAllowedHat = false;
            uint256 len = p.pollHatIds.length;
            for (uint256 i = 0; i < len;) {
                if (l.hats.isWearerOfHat(voter, p.pollHatIds[i])) {
                    hasAllowedHat = true;
                    break;
                }
                unchecked {
                    ++i;
                }
            }
            if (!hasAllowedHat) revert VotingErrors.RoleNotAllowed();
        }

        if (p.hasVoted[voter]) revert VotingErrors.AlreadyVoted();

        // Validate weights
        VotingMath.validateWeights(VotingMath.Weights({idxs: idxs, weights: weights, optionsLen: p.options.length}));

        // Effects-before-Interactions: flip hasVoted BEFORE any external call
        // (IERC20.balanceOf inside _calculateClassPower can land on a token
        // contract whose code we don't control). The cited balanceOf-callback
        // attack vector is not exploitable today because Solidity emits
        // STATICCALL for IERC20.balanceOf, which the EVM enforces no-state-
        // modification on the entire call subtree. CEI ordering remains
        // correct hygiene and forward-defense against future class strategies
        // that use non-view calls on cls.asset. If the outer tx later reverts
        // (e.g. via the zero-power check below), the EVM rolls hasVoted back
        // atomically, so honest callers are unaffected. See
        // test/HybridVotingReentrancy.t.sol for the property test.
        p.hasVoted[voter] = true;

        // Calculate raw power for each class
        uint256 classCount = p.classesSnapshot.length;
        uint256[] memory classRawPowers = new uint256[](classCount);

        for (uint256 c; c < classCount;) {
            HybridVoting.ClassConfig memory cls = p.classesSnapshot[c];
            uint256 rawPower = _calculateClassPower(voter, cls, l);
            classRawPowers[c] = rawPower;
            p.classTotalsRaw[c] += rawPower;
            unchecked {
                ++c;
            }
        }

        // Reject voters with zero power across all classes (prevents quorum inflation via Sybil)
        {
            bool hasAnyPower = false;
            for (uint256 c2; c2 < classCount;) {
                if (classRawPowers[c2] > 0) {
                    hasAnyPower = true;
                    break;
                }
                unchecked {
                    ++c2;
                }
            }
            if (!hasAnyPower) revert VotingErrors.Unauthorized();
        }

        // Accumulate deltas for each option
        uint256 len2 = weights.length;
        for (uint256 i; i < len2;) {
            uint8 ix = idxs[i];
            uint8 weight = weights[i];

            for (uint256 c; c < classCount;) {
                if (classRawPowers[c] > 0) {
                    uint256 delta = (classRawPowers[c] * weight) / 100;
                    if (delta > 0) {
                        uint256 newVal = p.options[ix].classRaw[c] + delta;
                        require(VotingMath.fitsUint128(newVal), "Class raw overflow");
                        p.options[ix].classRaw[c] = uint128(newVal);
                    }
                }
                unchecked {
                    ++c;
                }
            }
            unchecked {
                ++i;
            }
        }

        unchecked {
            p.voterCount++;
        }
        emit VoteCast(id, voter, idxs, weights, classRawPowers, uint64(block.timestamp));
    }

    /// Early-close gate (Proposal #60, redesigned). Returns true iff turnout
    /// has reached the org-configured threshold AND the proposal-level quorum
    /// (if set) is satisfied. No majority / score check at the gate —
    /// announceWinner's existing winner-picking and validity logic remains
    /// the final arbiter of who wins.
    ///
    /// Conditions (all must hold):
    ///   1. snapshotEligibleVoters is an active value (not 0 — legacy
    ///      pre-upgrade proposals stay timer-only; not type(uint64).max —
    ///      explicit timer-only opt-out at create time).
    ///   2. voterCount has reached ceil(snapshotEligibleVoters * pct / 100)
    ///      where pct is the proposal's turnoutPctOverride if set, else the
    ///      org-level earlyCloseTurnoutPct. Both 0 means 100 (safe default —
    ///      strict full-turnout early-close).
    ///   3. Quorum (if set) is satisfied — mirrors announceWinner's check so
    ///      the gate doesn't fire on a path that would invalidate.
    ///
    /// Why no majority check: orgs choose their own turnout floor (100% means
    /// "wait for everyone" — no disenfranchisement risk; lower values let the
    /// org trade deliberation for speed at their discretion). A hardcoded
    /// "strict majority" requirement on top would override the org's
    /// configured trust model. If turnout is reached but no option has a valid
    /// winner under thresholdPct / strict-margin rules, announceWinner returns
    /// (0, false) — the gate is permission to attempt announce, not a
    /// guarantee of validity.
    function _isEarlyCloseEligible(uint256 id) internal view returns (bool) {
        HybridVoting.Layout storage l = _layout();
        if (id >= l._proposals.length) return false;
        HybridVoting.Proposal storage p = l._proposals[id];

        if (p.snapshotEligibleVoters == 0) return false;
        if (p.snapshotEligibleVoters == type(uint64).max) return false;

        // Resolve effective turnout percent: per-proposal override > org default;
        // 0 (legacy back-compat sentinel) -> 100.
        uint8 pct = p.turnoutPctOverride != 0 ? p.turnoutPctOverride : l.earlyCloseTurnoutPct;
        if (pct == 0) pct = 100;

        // Ceiling division: threshold = ceil(eligible * pct / 100).
        // Max eligible (uint64) * 100 = 1.8e21, well under uint256.max.
        uint256 threshold = (uint256(p.snapshotEligibleVoters) * pct + 99) / 100;
        if (uint256(p.voterCount) < threshold) return false;

        if (l.quorum > 0 && p.voterCount < l.quorum) return false;

        return true;
    }

    function _calculateClassPower(address voter, HybridVoting.ClassConfig memory cls, HybridVoting.Layout storage l)
        internal
        view
        returns (uint256)
    {
        // Check hat gating for this class
        bool hasClassHat = (voter == address(l.executor)) || (cls.hatIds.length == 0);

        // Check if voter has any of the class hats
        if (!hasClassHat && cls.hatIds.length > 0) {
            for (uint256 i; i < cls.hatIds.length;) {
                if (l.hats.isWearerOfHat(voter, cls.hatIds[i])) {
                    hasClassHat = true;
                    break;
                }
                unchecked {
                    ++i;
                }
            }
        }

        if (!hasClassHat) return 0;

        if (cls.strategy == HybridVoting.ClassStrategy.DIRECT) {
            return 100; // Direct democracy: 1 person = 100 raw points
        } else if (cls.strategy == HybridVoting.ClassStrategy.ERC20_BAL) {
            uint256 balance = IERC20(cls.asset).balanceOf(voter);
            if (balance < cls.minBalance) return 0;
            uint256 power = cls.quadratic ? VotingMath.sqrt(balance) : balance;
            return power * 100; // Scale to match existing system
        }

        return 0;
    }

    function announceWinner(uint256 id) external returns (uint256 winner, bool valid) {
        HybridVoting.Layout storage l = _layout();
        HybridVoting.Proposal storage p = l._proposals[id];
        if (p.executed) revert VotingErrors.AlreadyExecuted();
        p.executed = true;

        // Check if any votes were cast
        bool hasVotes = false;
        for (uint256 i; i < p.classTotalsRaw.length;) {
            if (p.classTotalsRaw[i] > 0) {
                hasVotes = true;
                break;
            }
            unchecked {
                ++i;
            }
        }

        if (!hasVotes) {
            emit Winner(id, 0, false, false, uint64(block.timestamp));
            return (0, false);
        }

        // Check quorum: minimum number of voters required
        if (l.quorum > 0 && p.voterCount < l.quorum) {
            emit Winner(id, 0, false, false, uint64(block.timestamp));
            return (0, false);
        }

        // Build matrix for N-class winner calculation
        uint256 numOptions = p.options.length;
        uint256 numClasses = p.classesSnapshot.length;
        uint256[][] memory perOptionPerClassRaw = new uint256[][](numOptions);
        uint8[] memory slices = new uint8[](numClasses);

        for (uint256 opt; opt < numOptions;) {
            perOptionPerClassRaw[opt] = new uint256[](numClasses);
            for (uint256 cls; cls < numClasses;) {
                perOptionPerClassRaw[opt][cls] = p.options[opt].classRaw[cls];
                unchecked {
                    ++cls;
                }
            }
            unchecked {
                ++opt;
            }
        }

        for (uint256 cls; cls < numClasses;) {
            slices[cls] = p.classesSnapshot[cls].slicePct;
            unchecked {
                ++cls;
            }
        }

        // Use VotingMath to pick winner with N-class logic
        (winner, valid,,) = VotingMath.pickWinnerNSlices(
            perOptionPerClassRaw,
            p.classTotalsRaw,
            slices,
            l.thresholdPct,
            true // strict majority required
        );

        IExecutor.Call[] storage batch = p.batches[winner];
        bool didExecute = false;
        if (valid && batch.length > 0) {
            try l.executor.execute(id, batch) {
                didExecute = true;
                emit ProposalExecuted(id, winner, batch.length);
            } catch (bytes memory reason) {
                emit ProposalExecutionFailed(id, winner, reason);
            }
        }
        emit Winner(id, winner, valid, didExecute, uint64(block.timestamp));
    }
}
